BEGIN(HEADER)
    PROPERTY(INT "textures[0]" 0)
    PROPERTY(INT "textures[1]" 1)
END(HEADER)

BEGIN(PASS)
    SET(ITERATION ONCE)

    SET(ATTRIBUTE POSITION "vertex_position")
    SET(ATTRIBUTE DIFFUSE "vertex_diffuse")
    SET(ATTRIBUTE TEXCOORD0 "texture_coord0")
    SET(ATTRIBUTE TEXCOORD1 "texture_coord1")

    SET(UNIFORM MODELVIEW_PROJECTION_MATRIX "modelview_projection")
    SET(UNIFORM LIGHT_GLOBAL_AMBIENT "global_ambient")
    SET(UNIFORM MATERIAL_AMBIENT "material_ambient")
    SET(UNIFORM MATERIAL_DIFFUSE "material_diffuse")
    SET(UNIFORM POINT_SIZE "point_size")
    SET(UNIFORM TEXTURE_MATRIX0 "texture_matrix[0]")
    SET(UNIFORM TEXTURE_MATRIX1 "texture_matrix[1]")
    SET(UNIFORM ACTIVE_TEXTURE_UNITS "active_texture_count")

    BEGIN_DATA(VERTEX)
        #version 120
        attribute vec3 vertex_position;
        attribute vec4 vertex_diffuse;
        attribute vec2 texture_coord0;
        attribute vec2 texture_coord1;

        uniform mat4 modelview_projection;
        uniform float point_size;
        uniform mat4 texture_matrix[2];

        varying vec2 frag_texcoord0;
        varying vec2 frag_texcoord1;
        varying vec4 frag_diffuse;

        void main() {
            frag_texcoord0 = (texture_matrix[0] * vec4(texture_coord0, 0, 1)).st;
            frag_texcoord1 = (texture_matrix[1] * vec4(texture_coord1, 0, 1)).st;
            frag_diffuse = vertex_diffuse;
            gl_Position = (modelview_projection * vec4(vertex_position, 1.0));
            gl_PointSize = point_size;
        }
    END_DATA(VERTEX)

    BEGIN_DATA(FRAGMENT)
        #version 120
        uniform vec4 global_ambient;
        uniform vec4 material_ambient;
        uniform vec4 material_diffuse;
        uniform sampler2D textures[2];
        uniform mat4 texture_matrix[2];
        uniform int active_texture_count;

        varying vec2 frag_texcoord0;
        varying vec2 frag_texcoord1;
        varying vec4 frag_diffuse;

        void main() {
            vec4 color = (global_ambient + material_ambient) * frag_diffuse;
            if(active_texture_count == 0) {
                gl_FragColor = color;
            } else {
                vec4 t1 = texture2D(textures[0], frag_texcoord0.st) * material_diffuse;
                if(active_texture_count == 1) {
                    gl_FragColor = t1 * color;
                } else {
                    vec4 t2 = texture2D(textures[1], frag_texcoord1.st) * material_diffuse;
                    gl_FragColor = (t1 + vec4(t2.rgb, 1.0 - t1.a) * color);
                }
            }
        }
    END_DATA(FRAGMENT)
END(PASS)
BEGIN(PASS)
    SET(ITERATION ONCE_PER_LIGHT)
    SET(MAX_ITERATIONS 8)

    SET(ATTRIBUTE POSITION "vertex_position")
    SET(ATTRIBUTE NORMAL "vertex_normal")
    SET(ATTRIBUTE DIFFUSE "vertex_diffuse")

    SET(UNIFORM VIEW_MATRIX "view")
    SET(UNIFORM MODELVIEW_MATRIX "modelview")
    SET(UNIFORM MODELVIEW_PROJECTION_MATRIX "modelview_projection")
    SET(UNIFORM NORMAL_MATRIX "normal_matrix")

    SET(UNIFORM LIGHT_POSITION "light_position")
    SET(UNIFORM LIGHT_DIFFUSE "light_diffuse")
    SET(UNIFORM LIGHT_SPECULAR "light_specular")

    SET(UNIFORM MATERIAL_SHININESS "material_shininess")
    SET(UNIFORM MATERIAL_DIFFUSE "material_diffuse")
    SET(UNIFORM MATERIAL_SPECULAR "material_specular")

    SET(UNIFORM LIGHT_CONSTANT_ATTENUATION "constant_attenuation")
    SET(UNIFORM LIGHT_LINEAR_ATTENUATION "linear_attenuation")
    SET(UNIFORM LIGHT_QUADRATIC_ATTENUATION "quadratic_attenuation")

    SET(FLAG BLEND ADD)

    BEGIN_DATA(VERTEX)
        #version 120
        attribute vec3 vertex_position;
        attribute vec4 vertex_diffuse;
        attribute vec3 vertex_normal;

        uniform mat4 modelview;
        uniform mat4 modelview_projection;
        uniform mat4 view;
        uniform mat3 normal_matrix;
        uniform vec4 light_position;

        varying vec4 vertex_position_eye;
        varying vec4 vertex_normal_eye;
        varying vec4 light_position_eye;
        varying vec4 frag_diffuse;

        void main() {
            vertex_normal_eye = vec4(normalize(normal_matrix * vertex_normal), 0); //Calculate the normal
            vertex_position_eye = (modelview * vec4(vertex_position, 1.0));
            light_position_eye = (view * light_position);

            frag_diffuse = vertex_diffuse;

            gl_Position = (modelview_projection * vec4(vertex_position, 1.0));
        }
    END_DATA(VERTEX)
    BEGIN_DATA(FRAGMENT)
        #version 120
        uniform vec4 light_diffuse;
        uniform vec4 light_specular;

        uniform vec4 material_diffuse;
        uniform vec4 material_specular;
        uniform float material_shininess;

        uniform float constant_attenuation;
        uniform float linear_attenuation;
        uniform float quadratic_attenuation;

        varying vec4 vertex_position_eye;
        varying vec4 vertex_normal_eye;
        varying vec4 light_position_eye;
        varying vec4 frag_diffuse;

        void main() {
            vec4 light_dir = light_position_eye - (vertex_position_eye * vec4(light_position_eye.w));
            vec4 n_eye = normalize(vertex_normal_eye);
            vec4 s_eye = normalize(light_dir);
            vec4 v_eye = normalize(-vertex_position_eye);
            vec4 h_eye = normalize(v_eye + s_eye);

            vec4 color = vec4(0);

            float intensity = max(dot(s_eye, n_eye), 0.0);

            if(intensity > 0.0) {
                float d = length(light_dir);
                float attenuation = 1.0;

                if(light_position_eye.w > 0.0) {
                    attenuation = 1.0 / (
                        constant_attenuation +
                        linear_attenuation * d +
                        quadratic_attenuation * d * d
                    );
                }

                color += attenuation * (light_diffuse * material_diffuse * intensity);
                float spec = max(dot(h_eye, n_eye), 0.0);
                color += attenuation * (light_specular * material_specular) * pow(spec, material_shininess) * intensity;
            }

            gl_FragColor = color * frag_diffuse;
        }

    END_DATA(FRAGMENT)
END(PASS)


